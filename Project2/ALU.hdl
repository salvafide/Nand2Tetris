// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // x[16] Zero out + NOT if selected, in series
    Mux16(a= x, b= false, sel= zx, out= muxX);
    Not16(in= muxX, out= notX);
    Mux16(a= muxX, b= notX, sel= nx, out= finalX);

    // y[16] Zero out + NOT if selected, in series
    Mux16(a= y, b= false, sel= zy, out= muxY);
    Not16(in= muxY, out= notY);
    Mux16(a= muxY, b= notY, sel= ny, out= finalY);

    // Pick between: (f == 1)Add x + y = XPlusY, x AND y = XAndY
    Add16(a = finalX, b = finalY, out = XPlusY);
    And16(a= finalX, b= finalY, out= XAndY);
    Mux16(a= XAndY, b= XPlusY, sel= f, out= funcResult);

    // if no == 1, negate funcResult
    Not16(in= funcResult, out= NegateResult);
    Mux16(a= funcResult, b= NegateResult, sel= no, out = out, out[0..7]= outLSB, out[8..15] = outMSB, out[15]= ng);

    // Compute zero and negative bit
    Or8Way(in[0..7]= outLSB, out= low8Or);
    Or8Way(in[0..7]= outMSB, out=high8Or);
    Or(a= low8Or, b= high8Or, out= zOr);
    Not(in= zOr, out= zr);
}